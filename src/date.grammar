Package com.platzhaltr.util.date;

Helpers
	january = ( 'Januar' | 'januar' | 'Jan' | 'jan' );
	february = ( 'Februar' | 'februar' | 'Feb' | 'feb');
	march = ( 'März' | 'märz' );
	april = ( 'April' | 'april' );
	may = ( 'Mai' | 'mai' );
	june = ( 'Juni' | 'juni' );
	july = ( 'Juli' | 'juli' );
	august = ( 'August' | 'august' | 'Aug' | 'aug' );
	september = ( 'September' | 'september' | 'Sep' | 'sep' );
	october = ( 'Oktober' | 'oktober' | 'Okt' | 'okt' );
	november = ( 'November' | 'november' | 'Nov' | 'nov' );
	december = ( 'Dezember' | 'dezember' | 'Dez' | 'dez' );

Tokens 
	next = ( 'Nächste' | 'Nächsten' | 'Nächstes' | 'Nächster' | 'nächste' | 'nächsten' | 'nächstes' | 'nächster' );
	
	monday = ( 'Montag' | 'montag' | 'Mo' | 'mo' );
	tuesday = ( 'Dienstag' | 'dienstag' | 'Di' | 'di' );
	wednesday = ( 'Mittwoch' | 'mittwoch' | 'Mi' | 'mi' );
	thursday = ( 'Donnerstag' | 'donnerstag' | 'Do' | 'do' );
	friday = ( 'Freitag' | 'freitag' | 'Fr' | 'fr' );
	saturday = ( 'Samstag' | 'samstag' | 'Sa' | 'sa' );
	sunday = ( 'Sonntag' | 'sonntag' | 'So' | 'so' );
	
	evening = ( 'Abend' | 'abend' | 'Abends' | 'abends' );
	
	weekend = ( 'Wochenende' | 'wochenende' | 'WE' | 'we' | 'We' );
	week = ( 'Woche' | 'woche' );
	month = ( 'Monat' | 'monat' );
	year = ( 'Jahr' | 'jahr' );
	
	every = ( 'Jeder' | 'jeder' | 'Jeden' | 'jeden' | 'Jedes' | 'jedes' );
	
	january = january;
	february = february;
	march = march;
	april = april;
	may = may;
	june = june;
	july = july;
	august = august;
	september = september;
	october = october;
	november = november;
	december = december;
	
	dot = '.';
	colon = ':';
	on = ( 'Am' | 'am' );
	in = ( 'In' | 'in' );
	at = ( 'um' );
	oclock = ( 'Uhr' | 'uhr' );
	
	blank = (' ' | 13 | 10)+;
	subject = [['a'..'z'] + ['A'..'Z']]+;
	number = ['0' .. '9']+;

Ignored Tokens 
	blank;


Productions

entry {-> entry} =
	{absolute}	on absolute time? { -> New entry.date(absolute.date, time.time) }
|	{relative}	prefix relative time? { -> New entry.relative(relative.relative, time.time) }
|	{repeating} T.every [count]:P.number? relative time? {-> New entry.repeating(count.number, relative.relative, time.time) }
;

prefix =
	{on} on
|	{next} next
;

absolute {-> date} =
	{latin}		[date]:date {-> New date.latin([date.number]) }
|	{january}	[date]:P.number T.january [year]:T.number? {-> New date.january([date.number, year]) }
|	{february}	[date]:P.number T.february [year]:T.number? {-> New date.february([date.number, year]) }
|	{march}		[date]:P.number T.march [year]:T.number? {-> New date.march([date.number, year]) }
|	{april}		[date]:P.number T.april [year]:T.number? {-> New date.april([date.number, year]) }
|	{may}		[date]:P.number T.may [year]:T.number? {-> New date.may([date.number, year]) }
|	{june}		[date]:P.number T.june [year]:T.number? {-> New date.june([date.number, year]) }
|	{july}		[date]:P.number T.july [year]:T.number? {-> New date.july([date.number, year]) }
|	{august}	[date]:P.number T.august [year]:T.number? {-> New date.august([date.number, year]) }
|	{september}	[date]:P.number T.september [year]:T.number? {-> New date.september([date.number, year]) }
|	{october}	[date]:P.number T.october [year]:T.number? {-> New date.october([date.number, year]) }
|	{november}	[date]:P.number T.november [year]:T.number? {-> New date.november([date.number, year]) }
|	{december}	[date]:P.number T.december [year]:T.number? {-> New date.december([date.number, year]) }
;

time {-> time} =
	{absolute}	at [hour]:T.number T.colon? [minute]:T.number? oclock {-> New time.absolute(hour, minute) }
|	{evening}	T.evening {-> New time.evening() }
;

date {-> T.number*} =
	{single}	[day]:T.number [dot1]:T.dot [month]:T.number [dot2]:T.dot [year]:T.number? {-> [day, month, year] }
;

relative {-> relative} =
	{workingday} workingday	{-> workingday.relative}
|	{weekendday} weekendday	{-> weekendday.relative}
|	{weekend} 	weekend		{-> New relative.weekend()}
|	{week}		week		{-> New relative.week()}
|	{month}		month		{-> New relative.month()}
|	{year}		year		{-> New relative.year()}
;

workingday { -> relative } =
	{monday} 	monday		{-> New relative.monday() }
|	{tuesday}	tuesday	{-> New relative.tuesday() }
|	{wednesday}	wednesday	{-> New relative.wednesday() }
|	{thursday}	thursday	{-> New relative.thursday() }
|	{friday}	friday		{-> New relative.friday() }
;

weekendday { -> relative } =
	{saturday}	saturday	{-> New relative.saturday() }
|	{sunday}	sunday		{-> New relative.sunday() }
;

number { -> number } =
	T.number T.dot	{-> number }
;

Abstract Syntax Tree

entry =
	{next}	relative time?
|	{relative}	relative time?
|	{date}		date time?
|	{repeating}	T.number? relative time?
;

relative =
	{monday}	
|	{tuesday}	
|	{wednesday}	
|	{thursday}	
|	{friday}	
|	{saturday}	
|	{sunday}	
|	{weekend} 	
|	{week}		
|	{month}		
|	{year}		
;

date =
	{latin}		T.number+
|	{january}	T.number+
|	{february}	T.number+
|	{march}		T.number+
|	{april}		T.number+
|	{may}		T.number+
|	{june}		T.number+
|	{july}		T.number+
|	{august}	T.number+
|	{september}	T.number+
|	{october}	T.number+
|	{november}	T.number+
|	{december}	T.number+
;

time = 
	{absolute}	[hour]:T.number [minute]:T.number?
|	{evening}
// TODO extend
; 	